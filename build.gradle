plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "org.beryx.jlink" version "2.17.7"

}

group 'com.inv'
version '1.0.0'

project.description = "Invoicing and Sales System."
project.ext.buildDate = new Date()
project.version = "1.0"

application{
   mainModule= 'ILite'
   mainClass = 'com.inv.Launch'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
    implementation group: 'commons-dbutils', name: 'commons-dbutils', version: '1.7'
    implementation name: 'iconsfx-1.11'
    implementation name: 'xctrl-1.11'
    implementation name: 'XPieChart-1.11'
    implementation name: 'xtext-1.0'
    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13.1'
    implementation 'com.intellij:annotations:12.0@jar'

}

javafx {
    version = "11"
    modules = [ 'javafx.controls','javafx.fxml', 'javafx.media','javafx.graphics' , 'javafx.base','javafx.web']
}




jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    launcher {
        name = 'ILite'
        jvmArgs = ['-XX:+UnlockExperimentalVMOptions','-XX:+UseZGC','-Xms64m','-Xmx1500m' ,'-Djdk.gtk.version=2']
    }
    forceMerge('log4j-api')
    jpackage {
        installerOptions = [
                '--description', project.description,
                '--copyright', 'Copyrigth 2020 ',
                '--app-version','1.0.0'
        ]

        installerType = project.findProperty('installerType') // we will pass this from the command line (example: -PinstallerType=msi)
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/assets/ico.ico']
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/assets/ico.ico']
        }
        if (installerType in ['deb', 'rpm']) {
            imageOptions += ['--icon', 'src/main/resources/assets/icon.png']
            installerOptions += [
                    '--linux-menu-group', 'ILite',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'deb') {
            installerOptions += [
                    '--linux-deb-maintainer', 'imxdsswar@gmail.com'
            ]
        }
        if (installerType == 'rpm') {
            installerOptions += [
                    '--linux-rpm-license-type', 'GPLv3'
            ]
        }
    }
}



jpackage {
    doFirst {
        project.getProperty('installerType') // throws exception if its missing
    }
}